C MODPATH-PLOT Version 3.00 (V3, Release 2, 5-99)
C Changes:
C   No change since previous release: (V3, Release 1, 9-94)
C***** SUBROUTINES *****
C     KNTCOM
C     LNSKIP
C     VERNUM
C     READEP
C     READTS
C     READPL
C     GETRFT
C***********************
 
C***** SUBROUTINE *****
      SUBROUTINE KNTCOM(IU,N,TOKEN,LINE,IERR)
      CHARACTER*1 TOKEN
      CHARACTER*(*) LINE
      IERR=0
      N=0
      REWIND(IU)
10    READ(IU,'(A)',END=200) LINE
      IF(LINE(1:1).NE.TOKEN) GO TO 100
      N=N+1
      GO TO 10
100   REWIND(IU)
      IF(N.GT.0) READ(IU,'(A)') LINE
      REWIND(IU)
      RETURN
200   IERR=1
      REWIND(IU)
      RETURN
      END
 
C***** SUBROUTINE *****
      SUBROUTINE LNSKIP(IU,N,IERR)
      CHARACTER*1 X
      IERR=0
      DO 10 I=1,N
      READ(IU,'(A1)',END=100) X
10    CONTINUE
      RETURN
100   IERR=1
      RETURN
      END
 
C***** SUBROUTINE *****
      SUBROUTINE VERNUM(IU,ICMPCT,NCOM,TREF,IVER)
      CHARACTER*132 LINE1,LINE
C
      LINE1= ' '
      line= ' '
      IERR1=0
      IERR2=0
      IVER=100
      TREF=0.0
      IF(ICMPCT.LT.2) THEN
      CALL KNTCOM(IU,NCOM,'@',LINE1,IERR1)
C
      IF(NCOM.GT.0) THEN
        CALL UPCASE(LINE1)
        N2=INDEX(LINE1,']')
        IF(LINE1(3:3).EQ.'[' .AND. N2.GT.5) THEN
          LINE= LINE1(4:N2-1)
          NVER= INDEX(LINE,'VERSION 3.')
          NIJK= INDEX(LINE,'COMPACT')
          IF(NVER.GT.0 .AND. NIJK.EQ.0) THEN
            IVER=3000
          ELSE IF(NVER.GT.0 .AND. NIJK.GT.0) THEN
            IVER=3001
          END IF
          CALL GETRFT(LINE,TREF)
        END IF
      END IF
C
      REWIND(IU)
      IF(NCOM.GT.0) CALL LNSKIP(IU,NCOM,IERR2)
      IF(IERR1.NE.0 .OR. IERR2.NE.0) IVER=0
 
C
      ELSE IF(ICMPCT.EQ.2) THEN
        READ(IU) LINE,TREF
        CALL UPCASE(LINE)
        NVER=INDEX(LINE,'VERSION 3.')
        IF(NVER.GT.0) THEN
          IVER=3002
        ELSE
          IVER=0
        END IF
      ELSE
        IVER=0
      END IF
 
      RETURN
      END
 
C***** SUBROUTINE *****
      SUBROUTINE READEP(IU,IVER,ICMPCT,IZL,JLAST,ILAST,KLAST,XLAST,
     1  YLAST,ZLLAST,T,XFRST,YFRST,ZLFRST,JFRST,IFRST,KFRST,IZF,NSFRST,
     2  IDCODE,TRLEAS,NSTEP,NCOL,NROW,IEOF)
      INTEGER*4 IPCODE,NFRST,NLAST,IZF4,IZL4,NSF4
C
      IEOF=0
C
      IF(IVER.EQ.0) CALL VERNUM(IU,ICMPCT,NCOM,TREF,IVER)
      IF(IVER.LT.100) RETURN
 
C... READ ONE OF THE ORIGINAL MODPATH ENDPOINT FILES
      IF(IVER.EQ.100) THEN
        READ(IU,*,END=20 ) IZL,JLAST,ILAST,KLAST,XLAST,YLAST,ZLAST,
     1   ZLLAST,T,XFRST,YFRST,ZLFRST,JFRST,IFRST,KFRST,IZF
        IDCODE=1
        TRLEAS=0.0
        NSTEP=1
        NSFRST=1
 
C... READ THE STANDARD FORM OF THE VERSION 3 ENDPOINT FILE
      ELSE IF(IVER.EQ.3000) THEN
        READ(IU,*,END=20 ) IZL,JLAST,ILAST,KLAST,XLAST,YLAST,ZLAST,
     1   ZLLAST,T,XFRST,YFRST,ZLFRST,JFRST,IFRST,KFRST,IZF,NSFRST,
     2   IPCODE,TRLEAS
        IF(IPCODE.LT.0) THEN
          IDCODE=IPCODE
          NSTEP=0
        ELSE
          IDCODE= MOD(IPCODE,10)
          NSTEP=IPCODE/10
        END IF
 
C... READ THE COMPACT FORM OF THE VERSION 3 ENDPOINT FILE
      ELSE IF(IVER.EQ.3001) THEN
        READ(IU,*,END=20 ) IZL,NLAST,XLAST,YLAST,ZLLAST,T,
     1   XFRST,YFRST,ZLFRST,NFRST,IZF,NSFRST,IPCODE,TRLEAS
        IF(IPCODE.LT.0) THEN
          IDCODE=IPCODE
          NSTEP=0
        ELSE
          IDCODE= MOD(IPCODE,10)
          NSTEP=IPCODE/10
        END IF
        CALL ND2IJK(NFRST,IFRST,JFRST,KFRST,NROW,NCOL)
        CALL ND2IJK(NLAST,ILAST,JLAST,KLAST,NROW,NCOL)
 
C... READ THE BINARY FORM OF THE VERSION 3 ENDPOINT FILE
      ELSE IF(IVER.EQ.3002) THEN
        READ(IU,END=20) IZL4,NLAST,XLAST,YLAST,ZLLAST,T,
     1   XFRST,YFRST,ZLFRST,NFRST,IZF4,NSF4,IPCODE,TRLEAS
        IZL=IZL4
        IZF=IZF4
        NSFRST=NSF4
        IF(IPCODE.LT.0) THEN
          IDCODE=IPCODE
          NSTEP=0
        ELSE
          IDCODE= MOD(IPCODE,10)
          NSTEP=IPCODE/10
        END IF
        CALL ND2IJK(NFRST,IFRST,JFRST,KFRST,NROW,NCOL)
        CALL ND2IJK(NLAST,ILAST,JLAST,KLAST,NROW,NCOL)
 
      END IF
 
C
      RETURN
20    IEOF=1
      RETURN
      END
 
C***** SUBROUTINE *****
      SUBROUTINE READTS(IU,IVER,ICMPCT,KNT,N,J,I,K,XX,YY,ZL,ZLL,TIME,
     1                  NSTEP,NROW,NCOL,IERR)
      INTEGER*4 NODE,N4,NSTP4
 
      IERR=0
      IF(IVER.EQ.0) CALL VERNUM(IU,ICMPCT,NCOM,TREF,IVER)
      IF(IVER.LT.100) RETURN
C... READ ON OF THE ORIGINAL MODPATH TIMESERIES FILES
      IF(IVER.EQ.100) THEN
        NSTEP=1
        READ(IU,*,END=20) KNT,N,J,I,K,XX,YY,ZL,ZLL,TIME
 
C... READ THE REGULAR FORM OF THE VERSION 3 TIMESERIES FILE
      ELSE IF(IVER.EQ.3000) THEN
        READ(IU,*,END=20) KNT,N,J,I,K,XX,YY,ZL,ZLL,TIME,NSTEP
 
C... READ THE COMPACT FORM OF THE VERSION 3 TIMESERIES FILE
      ELSE IF(IVER.EQ.3001) THEN
        READ(IU,*,END=20) KNT,N,NODE,XX,YY,ZL,ZLL,TIME,NSTEP
        CALL ND2IJK(NODE,I,J,K,NROW,NCOL)
 
C... READ THE BINARY FORM OF THE VERSION 3 TIMESERIES FILE
      ELSE IF(IVER.EQ.3002) THEN
        READ(IU,END=20) KNT,N4,NODE,XX,YY,ZL,ZLL,TIME,NSTP4
        N=N4
        NSTEP=NSTP4
        CALL ND2IJK(NODE,I,J,K,NROW,NCOL)
      END IF
 
      RETURN
 
20    IERR=1
      RETURN
      END
 
C***** SUBROUTINE *****
      SUBROUTINE READPL(IU,IVER,ICMPCT,N,X,Y,ZL,Z,T,J,I,K,NSTEP,
     1                  NROW,NCOL,TREF,IERR)
      INTEGER*4 NODE,N4,NSTP4
 
      IERR=0
C
      IF(IVER.EQ.0) CALL VERNUM(IU,ICMPCT,NCOM,TREF,IVER)
      IF(IVER.LT.100) RETURN
 
C... READ THE ORIGINAL MODPATH PATHLINE FILE
      IF(IVER.EQ.100) THEN
        NSTEP=1
        READ(IU,*,END=20) N,X,Y,ZL,Z,T,J,I,K
 
C... READ THE REGULAR VERSION 3 PATHLINE FILE
      ELSE IF(IVER.EQ.3000) THEN
        READ(IU,*,END=20) N,X,Y,ZL,Z,T,J,I,K,NSTEP
 
C... READ THE COMPACT VERSION 3 PATHLINE FILE
      ELSE IF(IVER.EQ.3001) THEN
        READ(IU,*,END=20) N,X,Y,ZL,Z,T,NODE,NSTEP
        CALL ND2IJK(NODE,I,J,K,NROW,NCOL)
 
C... READ THE BINARY FORM OF THE VERSION 3 PATHLINE FILE
      ELSE IF(IVER.EQ.3002) THEN
        READ(IU,END=20) N4,X,Y,ZL,Z,T,NODE,NSTP4
        N=N4
        NSTEP=NSTP4
        CALL ND2IJK(NODE,I,J,K,NROW,NCOL)
      END IF
 
      RETURN
 
20    IERR=1
      RETURN
      END
 
C***** SUBROUTINE *****
      SUBROUTINE GETRFT(LINE2,T)
      CHARACTER*(*) LINE2
      CHARACTER*132 LINE
      T=0
      LINE=LINE2
      CALL UPCASE(LINE)
      N1=INDEX(LINE,'(TREF=')
      IF(N1.LE.0) GO TO 100
      ICOL=N1+6
      CALL URWORD(LINE,ICOL,ISTART,ISTOP,3,IDUM,T,-1,0)
100   RETURN
      END
